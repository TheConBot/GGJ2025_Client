mapscripts Treetop_House1_MapScripts{}

script Treetop_House1_TorkoalLady
{
	lock
	faceplayer
	if(flag(FLAG_FORTREE_NPC_TRADE_COMPLETED))
	{
		msgbox(t_Treetop_House1_GoingToMakeTorkoalStrong, MSGBOX_DEFAULT)
		release
		end
	}
	setvar(VAR_0x8008, INGAME_TRADE_TORKOAL)
	copyvar(VAR_0x8004, VAR_0x8008)
	specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
	copyvar(VAR_0x8009, VAR_RESULT)
	msgbox(t_Treetop_House1_YouWillTradeWontYou, MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO)
	{
		msgbox(t_Treetop_House1_YouWontTradeMe, MSGBOX_DEFAULT)
		release
		end
	}
	special(ChoosePartyMon)
	waitstate

	copyvar(VAR_0x800A, VAR_0x8004)
	if(var(VAR_0x8004) == PARTY_NOTHING_CHOSEN)
	{
		msgbox(t_Treetop_House1_YouWontTradeMe, MSGBOX_DEFAULT)
		release
		end
	}
	copyvar(VAR_0x8005, VAR_0x800A)
	specialvar(VAR_RESULT, GetTradeSpecies)
	copyvar(VAR_0x800B, VAR_RESULT)
	if(var(VAR_RESULT) != VAR_0x8009)
	{
		bufferspeciesname(STR_VAR_1, VAR_0x8009)
		msgbox(t_Treetop_House1_NotNinjask, MSGBOX_DEFAULT)
		release
		end
	}
	copyvar(VAR_0x8004, VAR_0x8008)
	copyvar(VAR_0x8005, VAR_0x800A)
	special(CreateInGameTradePokemon)
	special(DoInGameTradeScene)
	waitstate

	bufferspeciesname(STR_VAR_1, VAR_0x8009)
	msgbox(t_Treetop_House1_MonYouTakeCare, MSGBOX_DEFAULT)
	setflag(FLAG_FORTREE_NPC_TRADE_COMPLETED)
	release
	end
}

text t_Treetop_House1_YouWillTradeWontYou
{
    "Oh dear, I must have it!\n"
    "A NINJASK, so resplendent yet elusive!\l"
    "I'll trade my TORKOAL for it!\p"
    "Did you hear my plea?\n"
    "From the depths of my heart?\l"
    "I'll even let you in on a secret\p"
    "You can catch NINCADA on the forest floor.\n"
    "So won't you help me?$"
}

text t_Treetop_House1_MonYouTakeCare
{
    "Ah, thank you, kind soul!\p"
    "NINJASK, welcome!\n"
    "TORKOAL, be well with your new friend!$"
}

text t_Treetop_House1_NotNinjask
{
    "Oh no, this won't do.\n"
    "That isn't a NINJASK.$"
}

text t_Treetop_House1_YouWontTradeMe
{
    "Oh, you won't trade?\n"
    "Even after my heartfelt plea?$"
}

text t_Treetop_House1_GoingToMakeTorkoalStrong
{
    "You must make TORKOAL stronger\n"
    "to make yourself stronger too!$"
}
